apply plugin: 'java'
apply plugin: 'maven'


uploadArchives {
  repositories.mavenDeployer {
    repository(url: "file:///${System.properties['user.home']}/artifacts/")
    pom.version = "2.6.3"
    pom.artifactId = "briefj"
    pom.groupId = "com.github.UBC-Stat-ML"
  }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


apply plugin: 'eclipse'
eclipse { 
  classpath {
    defaultOutputDir = file('bin')
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    url "https://www.stat.ubc.ca/~bouchard/maven/"
  }
}

dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '18.0'
  testCompile group: 'junit', name: 'junit', version: '4.12'
  compile group: 'junit', name: 'junit', version: '4.12'
  compile 'net.sf.opencsv:opencsv:2.3'
  compile 'org.apache.commons:commons-math3:3.4.1'
  compile group: 'ca.ubc.stat', name: 'binc', version: '2.0.4'
  compile 'org.eclipse.jgit:org.eclipse.jgit:3.7.0.201502260915-r'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
  compile 'com.google.code.gson:gson:2.3.1'
  compile 'org.xerial:sqlite-jdbc:3.8.7'
  compile 'commons-io:commons-io:2.4'
  compile 'org.slf4j:slf4j-simple:1.6.1'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        //noinspection SpellCheckingInspection
        tasks.withType(Javadoc) {
            // disable the crazy super-strict doclint tool in Java 8
            //noinspection SpellCheckingInspection
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

